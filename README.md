# Лексический, синтаксический и семантический анализатор для учебного языка программирования на языке Python

## Структура

В файле main.py представлены основные части анализатора:

* lex_types - все виды лексем встречающиеся в учебном ЯП (ключевые слова, разделители, идентификаторы и числа);

* lex - структура выполняющая работу лексического анализатора;

* parser - структура выполняющая работу синтаксического и семантического анализатора.

## Примеры учебного ЯП

Приложенные файлы .txt представляют собой примеры реализации учебного ЯП, input.txt - демонстрирует работу анализатора, advanced_input.txt - файл содержащий все возможные операторы ЯП, остальные файлы представляют примеры синтаксических и семантических ошибок

## Грамматика

Грамматика учебного языка в форме Бэкуса - Наура:

#### 1. Операции языка

1.1. Операции группы «отношение»

* <операции_группы_отношения>::= <> | = | < | <= | > | >=

1.2. Операции группы «сложение»

* <операции_группы_сложения>::= + | - | or

1.3. Операции группы «умножение»

* <операции_группы_умножения>::= * | / | and

1.4. Унарная операция

* <унарная_операция>::= not


#### 2. Правила, определяющие структуру программы

2.1. Структура программы

* <программа>::= program var <описание> begin <оператор> {;<оператор>} end


#### 3. Правила, определяющие раздел описания переменных

3.1. Синтаксис команд описания данных

* <описание>::= <тип> <идентификатор> { , <идентификатор> }


#### 4. Правила, определяющие типы данных

4.1. Описание типов данных

* <тип>::= integer | real | boolean


#### 5. Правило, определяющее оператор программы

* <оператор>::= <составной> | <присваивания> | <условный> |
<фиксированного_цикла> | <условного_цикла> | <ввода> | <вывода>

5.1. Синтаксис составного оператора

* <составной>::= «[» <оператор> { ( : | перевод строки) <оператор> }
«]»

5.2. Синтаксис оператора присваивания

* <присваивания>::= <идентификатор> as <выражение>

5.3. Синтаксис оператора условного перехода

* <условный>::= if <выражение> then <оператор> [ else <оператор>]

5.4. Синтаксис оператора цикла с фиксированным числом повторений

* <фиксированного_цикла>::= for <присваивания> to <выражение> do
<оператор>

5.5. Синтаксис условного оператора цикла

* <условного_цикла>::= while <выражение> do <оператор>

5.6. Синтаксис оператора ввода

* <ввода>::= read «(»<идентификатор> {, <идентификатор> } «)»

5.7. Синтаксис оператора вывода

* <вывода>::= write «(»<выражение> {, <выражение> } «)»


#### 6. Многострочные комментарии в программе (шестая цифра варианта)

* { … }

#### Выражения языка задаются правилами:

* <выражение>::= <операнд>{<операции_группы_отношения> <операнд>}

* <операнд>::= <слагаемое> {<операции_группы_сложения> <слагаемое>}

* <слагаемое>::= <множитель> {<операции_группы_умножения> <множитель>}

* <множитель>::= <идентификатор> | <число> | <логическая_константа> |
<унарная_операция> <множитель> | «(»<выражение>«)»

* <число>::= <целое> | <действительное>

* <логическая_константа>::= true | false


#### Правила, определяющие идентификатор, букву и цифру:

* <идентификатор>::= <буква> {<буква> | <цифра>}

* <буква>::= A | B | C | D | E | F | G | H | I | J | K | L | M | N | O | P | Q | R | S | T | U | V
| W | X | Y | Z | a | b | c | d | e | f | g | h | i | j | k | l | m | n | o | p | q | r | s | t | u | v | w | x |
y | z

* <цифра>::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9


#### Правила, определяющие целые числа:

* <целое>::= <двоичное> | <восьмеричное> | <десятичное> |
<шестнадцатеричное>

* <двоичное>::= {/ 0 | 1 /} (B | b)

* <восьмеричное>::= {/ 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 /} (O | o)

* <десятичное>::= {/ <цифра> /} [D | d]

* <шестнадцатеричное>::= <цифра> {<цифра> | A | B | C | D | E | F |
a | b | c | d | e | f} (H | h)


#### Правила, описывающие действительные числа:

* <действительное>::= <числовая_строка> <порядок> |
[<числовая_строка>] . <числовая_строка> [порядок]

* <числовая_строка>::= {/ <цифра> /}

* <порядок>::= ( E | e )[+ | -] <числовая_строка>